#version 460 compatibility

layout(local_size_x = 128) in;

struct Particle
{
    vec3 position;
    float size;
    vec3 velocity;
    float age;
};

layout(std430, binding = 0) buffer Particles { Particle particles[]; };
layout(std430, binding = 1) buffer DeadList { uint dead_indices[]; };

uniform float delta_time;
uniform int max_particles;

void main()
{
    uint idx = gl_GlobalInvocationID.x;
    if(idx >= max_particles) return;

    if(particles[idx].age < 0) return;

    // Gravity
    particles[idx].velocity += vec3(0.0,-9.8,0.0) * delta_time;

    // Size decay
    particles[idx].size *= (1.0 - delta_time);
    if(particles[idx].size < 0) particles[idx].size = 0.0;

    // Age particles
    if((particles[idx].age += delta_time) > 1.0)
    {
        particles[idx].age = -1.0;

        uint dead_slot = atomicAdd(dead_indices[0], 1);
        dead_indices[dead_slot + 1] = idx;
    }
    else
    {
        particles[idx].position += particles[idx].velocity * delta_time;
    }
}