#version 460 compatibility

layout(local_size_x = 128) in;

struct Particle
{
    vec3 position;
    float size;
    vec3 velocity;
    float age;
    float life_length;
};

// Particle buffer
layout(std430, binding = 0) buffer Particles
{
    Particle particles[];
};

// Uniforms
uniform float delta_time;
uniform int max_particles;
uniform bool gravity;
uniform float size_falloff;

void main()
{
    uint idx = gl_GlobalInvocationID.x;
    if (idx >= max_particles) return;

    if (particles[idx].age < 0.0) return;

    // Gravity
    if(gravity) particles[idx].velocity += vec3(0.0, -9.8, 0.0) * delta_time;

    // Shrink size
    if(particles[idx].size > 0.0) particles[idx].size -= delta_time * size_falloff;
    if(particles[idx].size < 0.0)
    {
        particles[idx].size = 0.0;
        particles[idx].age = 1.0;
    }

    // Age particle
    particles[idx].age += delta_time/particles[idx].life_length;
    if(particles[idx].age > 1.0)
    {
        particles[idx].age = -1.0;
    }
    else
    {
        particles[idx].position += particles[idx].velocity * delta_time;
    }
}