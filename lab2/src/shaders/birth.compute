#version 460 compatibility

layout(local_size_x = 128) in;

struct Particle
{
    vec3 position;
    float size;
    vec3 velocity;
    float age;
    float life_length;
};

// Particle SSBO.
layout(std430, binding = 0) buffer Particles
{
    Particle particles[];
};

// Deadlist SSBO.
layout(std430, binding = 1) buffer DeadList
{
    uint dead_indices[];
};

// ### UNIFORM variables.
uniform int n_new_particles;
uniform int random;
uniform float maximum_life_length;
uniform float minimum_life_length;
uniform float start_velocity_strength;
uniform float maximum_start_size;
uniform float minimum_start_size;
uniform vec3 src_pstn; // Source position.
uniform vec3 src_r; // Radiuses per axis.

// Pseudo-random uint generator.
uint Hash(uint x)
{
    x ^= x >> 16;
    x *= 0x7feb352d;
    x ^= x >> 15;
    x *= 0x846ca68b;
    x ^= x >> 16;
    return x;
}

// Pseudo-random float (between 0 and 1) generator.
float Rand01(uint seed)
{
    return float(Hash(seed)) / 4294967295.0;
}

vec3 Position(uint seed)
{
    float x = Rand01(seed + 2) * 2.0 - 1.0;
    float y = Rand01(seed + 6) * 2.0 - 1.0;
    float z = Rand01(seed + 3) * 2.0 - 1.0;

    vec3 dir = normalize(vec3(x, y, z));

    float r = pow(Rand01(seed + 64), 1.0 / 3.0);

    vec3 offset = dir * r * src_r;

    return src_pstn + offset;
}

// Gives a random particle size.
float Size(uint seed)
{
    return minimum_start_size + Rand01(seed) * (maximum_start_size-minimum_start_size);
}

// Gives a random starting velocity normalized inside a unit sphere.
vec3 Velocity(uint seed)
{
    // Generate 3 random numbers in [-1, 1]
    float x = Rand01(seed)     * 2.0 - 1.0;
    float y = Rand01(seed + 1) * 2.0 - 1.0;
    float z = Rand01(seed + 2) * 2.0 - 1.0;

    vec3 dir = normalize(vec3(x, y, z));

    float speed = start_velocity_strength; // choose your speed
    return dir * speed;
}

void main()
{
    uint idx = gl_GlobalInvocationID.x;

    if(idx >= n_new_particles || idx >= dead_indices[0]) return;

    // Find the particle slot.
    uint slot = dead_indices[idx + 1];

    // Get a seed for this particular particle.
    uint seed = idx + random;

    // Initialize a new particle.
    particles[slot].position = Position(seed);
    particles[slot].size = Size(seed);
    particles[slot].velocity = Velocity(seed);
    particles[slot].age = 0.0;
    particles[slot].life_length = Rand01(seed+130303) * (maximum_life_length-minimum_life_length) + minimum_life_length + 0.000001;
}