#version 460 compatibility

layout(local_size_x = 128) in;

struct Particle
{
    vec3 position;
    float size;
    vec3 velocity;
    float age;
};

// Particle buffer
layout(std430, binding = 0) buffer Particles
{
    Particle particles[];
};

// Dead list buffer
layout(std430, binding = 1) buffer DeadList
{
    uint dead_indices[];
};

// Uniforms
uniform int n_new_particles;
uniform int random;

// Pseudo-random generator
uint Hash(uint x)
{
    x ^= x >> 16;
    x *= 0x7feb352d;
    x ^= x >> 15;
    x *= 0x846ca68b;
    x ^= x >> 16;
    return x;
}

float Rand01(uint seed)
{
    return float(Hash(seed)) / 4294967295.0;
}

vec3 Position(uint seed)
{
    return vec3(Rand01(seed) - 0.5, Rand01(seed + 1) - 0.5, Rand01(seed + 2) - 0.5);
}

float Size(uint seed)
{
    return 0.2 + Rand01(seed) * 5;
}

vec3 Velocity(uint seed)
{
    // Generate 3 random numbers in [-1, 1]
    float x = Rand01(seed)     * 2.0 - 1.0;
    float y = Rand01(seed + 1) * 2.0 - 1.0;
    float z = Rand01(seed + 2) * 2.0 - 1.0;

    vec3 dir = normalize(vec3(x, y, z));

    float speed = 5.0; // choose your speed
    return dir * speed;
}

void main()
{
    uint idx = gl_GlobalInvocationID.x;

    if(idx >= n_new_particles || idx >= dead_indices[0]) return;

    uint slot = dead_indices[idx + 1];

    uint seed = idx + random;

    particles[slot].position = Position(seed);
    particles[slot].size = Size(seed);
    particles[slot].velocity = Velocity(seed);
    particles[slot].age = 0.0;
}